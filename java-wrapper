#!/usr/bin/env nu
#
# Copyright (c) Jérémy Audiger.
# All rights reserved.
#

use mod.nu *

const MAVEN_GLOBAL_REPOSITORY_FOLDER = $"($nu.home-dir)/.m2/repository"
const GRADLE_GLOBAL_CACHE_FOLDERS = [
    $"($nu.home-dir)/.gradle/caches"
    $"($nu.home-dir)/.gradle/wrapper/dists"
    $"($nu.home-dir)/.gradle/daemon"
]
const MAVEN_LOCAL_BUILD_FOLDER = "target"
const GRADLE_LOCAL_BUILD_FOLDER = "build"
const GRADLE_LOCAL_CACHE_FOLDER = ".gradle"
const POM_FILE = "pom.xml"
const GRADLE_BUILD_FILE = "build.gradle"
const GRADLE_BUILD_KTS_FILE = "build.gradle.kts"

# Update the main pom version of the current directory
def "main pom version update" [
    --major # Update the major version
    --minor # Update the minor version
    --patch # Update the patch version
    --dry-run # Do not update the version
    --quiet # Do not print the new version
]: nothing -> string {
    let pom = open --raw $POM_FILE

    # Extract the current version
    let version = $pom
        | parse --regex '<version>(?<major>\d+).(?<minor>\d+).(?<patch>\d+)-RELEASE</version>'

    mut major_version = ($version.major.0 | into int)
    mut minor_version = ($version.minor.0 | into int)
    mut patch_version = ($version.patch.0 | into int)

    if $major {
        $major_version = $major_version + 1
        $minor_version = 0
        $patch_version = 0
    } else if $minor {
        $minor_version = $minor_version + 1
        $patch_version = 0
    } else if $patch {
        $patch_version = $patch_version + 1
    }

    if not $dry_run {
        let new_pom = $pom
            | str replace --regex "<version>[0-9.]+-RELEASE</version>" $"<version>($major_version).($minor_version).($patch_version)-RELEASE</version>"

        $new_pom
            | str trim --right
            | ($in + "\n")
            | save --raw --force $POM_FILE
    }

    if not $quiet {
        print $"New POM version: ($major_version).($minor_version).($patch_version)-RELEASE"
    }

    {
        major: $major_version,
        minor: $minor_version,
        patch: $patch_version
    } | to nuon
}

# Cleanup all the build directories of the current directory recursively
def "main cache cleanup" [
    --path: string=$GIT_FOLDER # The path to clean
    --dry-run # Do not cleanup the build directories
    --quiet # Do not print the build directories cleaned
]: nothing -> nothing {
    # Global cleanup
    if ($MAVEN_GLOBAL_REPOSITORY_FOLDER | path exists) {
        if not $quiet {
            print $"Cleaning global directory ($MAVEN_GLOBAL_REPOSITORY_FOLDER)"
        }

        if not $dry_run {
            rm -r $MAVEN_GLOBAL_REPOSITORY_FOLDER
        }
    }
    for folder in $GRADLE_GLOBAL_CACHE_FOLDERS {
        if ($folder | path exists) {
            if not $quiet {
                print $"Cleaning global directory ($folder)"
            }

            if not $dry_run {
                rm -r $folder
            }
        }
    }

    # Local cleanup
    let local_directories = glob $"($path)/**/{($MAVEN_LOCAL_BUILD_FOLDER),($GRADLE_LOCAL_BUILD_FOLDER),($GRADLE_LOCAL_CACHE_FOLDER)}"

    for local_directory in $local_directories {
        # Skip the directory if it is not part of a direct parent directory
        # (i.e. it is not a Maven or Gradle project)
        let parent = $"($local_directory)/.."
        if not (($parent | path join $POM_FILE | path exists) or ($parent | path join $GRADLE_BUILD_FILE | path exists) or ($parent | path join $GRADLE_BUILD_KTS_FILE | path exists)) {
            continue
        }

        if not $quiet {
            print $"Cleaning the directory ($local_directory)"
        }

        if $dry_run {
            continue
        }

        rm -r $local_directory
    }
}

def main []: nothing -> nothing {}
