#!/usr/bin/env nu
#
# Copyright (c) Jérémy Audiger.
# All rights reserved.
#

use mod.nu *
use std

const TY_GLOBAL_CACHE_FOLDER = $"($nu.home-dir)/.cache/ty"
const PYTHON_LOCAL_CACHE_FOLDER = "__pycache__"
const RUFF_LOCAL_CACHE_FOLDER = ".ruff_cache"
const VENV_LOCAL_CACHE_FOLDER = ".venv"

# Cleanup all the cache directories of the current directory recursively
def "main cache cleanup" [
    --path: string=$GIT_FOLDER # The path to clean
    --dry-run # Do not cleanup the build directories
    --quiet # Do not print the build directories cleaned
]: nothing -> nothing {
    # Global cleanup
    if not $quiet {
        print "Pruning pip cache"
    }
    if not $dry_run {
        ^pip cache purge out+err> (std null-device)
    }

    if not $quiet {
        print "Pruning uv cache"
    }
    if not $dry_run {
        ^uv cache prune out+err> (std null-device)
    }

    if ($TY_GLOBAL_CACHE_FOLDER | path exists) {
        if not $quiet {
            print $"Cleaning global directory ($TY_GLOBAL_CACHE_FOLDER)"
        }

        if not $dry_run {
            rm -rf $TY_GLOBAL_CACHE_FOLDER
        }
    }

    # Local cleanup
    let local_directories = glob $"($path)/**/{($PYTHON_LOCAL_CACHE_FOLDER),($RUFF_LOCAL_CACHE_FOLDER),($VENV_LOCAL_CACHE_FOLDER)}"

    for local_directory in $local_directories {
        if not $quiet {
            print $"Cleaning the directory ($local_directory)"
        }

        if $dry_run {
            continue
        }

        rm -r $local_directory
    }
}

def main []: nothing -> nothing {}
