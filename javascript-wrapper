#!/usr/bin/env nu
#
# Copyright (c) Jérémy Audiger.
# All rights reserved.
#

use mod.nu *
use std

const NPM_GLOBAL_CACHE_FOLDER = $"($nu.home-dir)/.npm"
const PNPM_GLOBAL_CACHE_FOLDER = $"($nu.home-dir)/.cache/pnpm"
const TYPESCRIPT_GLOBAL_CACHE_FOLDER = $"($nu.home-dir)/.cache/typescript"
const NEXT_LOCAL_BUILD_FOLDER = ".next"
const PNPM_LOCAL_STORE_FOLDER = ".pnpm-store"
const JS_LOCAL_BUILD_FOLDER = "dist"
const NODE_LOCAL_MODULES_FOLDER = "node_modules"
const PACKAGE_FILE = "package.json"

# Cleanup all the build directories of the current directory recursively
def "main cache cleanup" [
    --path: string=$GIT_FOLDER # The path to clean
    --dry-run # Do not cleanup the build directories
    --quiet # Do not print the build directories cleaned
]: nothing -> nothing {
    # Global cleanup
    if ($NPM_GLOBAL_CACHE_FOLDER | path exists) {
        if not $quiet {
            print $"Cleaning global directory ($NPM_GLOBAL_CACHE_FOLDER)"
        }

        if not $dry_run {
            rm -rf $NPM_GLOBAL_CACHE_FOLDER
        }
    }
    if ($PNPM_GLOBAL_CACHE_FOLDER | path exists) {
        if not $quiet {
            print $"Cleaning global directory ($PNPM_GLOBAL_CACHE_FOLDER)"
        }

        if not $dry_run {
            rm -rf $PNPM_GLOBAL_CACHE_FOLDER
        }
    }
    if ($TYPESCRIPT_GLOBAL_CACHE_FOLDER | path exists) {
        if not $quiet {
            print $"Cleaning global directory ($TYPESCRIPT_GLOBAL_CACHE_FOLDER)"
        }

        if not $dry_run {
            rm -rf $TYPESCRIPT_GLOBAL_CACHE_FOLDER
        }
    }

    if not $quiet {
        print "Pruning pnpm store"
    }
    if not $dry_run {
        ^pnpm store prune out+err> (std null-device)
    }

    # Local cleanup
    let local_directories = glob $"($path)/**/{($NEXT_LOCAL_BUILD_FOLDER),($PNPM_LOCAL_STORE_FOLDER),($JS_LOCAL_BUILD_FOLDER),($NODE_LOCAL_MODULES_FOLDER)}"

    for local_directory in $local_directories {
        # Skip the directory if it is not part of a direct parent directory
        # (i.e. directories are not at the root, or there is a .venv directory)
        let stats = ($local_directory | split row '/' | uniq --count)
        let dist = ($stats | find dist)
        let node_modules = ($stats | find --regex "^node_modules$")
        let venv = ($stats | find --regex "^.venv$")
        if ($venv | is-not-empty) {
            continue
        }
        if ($dist | is-not-empty) and ($node_modules | is-not-empty) {
            continue
        }
        if (($dist | is-not-empty) and ($dist | get count.0) > 1) or (($node_modules | is-not-empty) and ($node_modules | get count.0) > 1) {
            continue
        }

        # Skip the build directory if there is no configuration file at the root
        if not ($"($local_directory)/../($PACKAGE_FILE)" | path exists) {
            continue
        }

        if not $quiet {
            print $"Cleaning the directory ($local_directory)"
        }

        if $dry_run {
            continue
        }

        rm -r $local_directory
    }
}

def main []: nothing -> nothing {}
