#!/usr/bin/env nu
#
# Copyright (c) Jérémy Audiger.
# All rights reserved.
#

use mod.nu *

const NPM_CACHE_DIRECTORY = $"($nu.home-path)/.npm"
const PNPM_STORE_CACHE_DIRECTORY = $"($nu.home-path)/.pnpm-store"
const PNPM_CACHE_DIRECTORY = $"($nu.home-path)/.cache/pnpm"
const PACKAGE_FILE = "package.json"

# Cleanup all the build directories of the current directory recursively
def "main cache cleanup" [
    --path: string=$GIT_FOLDER # The path to clean
    --dry-run # Do not cleanup the build directories
    --quiet # Do not print the build directories cleaned
]: nothing -> nothing {
    # Toolings cleanup
    if ($NPM_CACHE_DIRECTORY | path exists) {
        if not $quiet {
            print $"Cleaning NPM global cache directory ($NPM_CACHE_DIRECTORY)"
        }

        if not $dry_run {
            rm -rf $NPM_CACHE_DIRECTORY
        }
    }
    if ($PNPM_STORE_CACHE_DIRECTORY | path exists) {
        if not $quiet {
            print $"Cleaning PNPM store cache directory ($PNPM_STORE_CACHE_DIRECTORY)"
        }

        if not $dry_run {
            rm -rf $PNPM_STORE_CACHE_DIRECTORY
        }
    }
    if ($PNPM_CACHE_DIRECTORY | path exists) {
        if not $quiet {
            print $"Cleaning PNPM global cache directory ($PNPM_CACHE_DIRECTORY)"
        }

        if not $dry_run {
            rm -rf $PNPM_CACHE_DIRECTORY
        }
    }

    # Local cleanup
    let local_directories = glob $"($path)/**/{.next,.pnpm-store,dist,node_modules}"

    for local_directory in $local_directories {
        # Skip the directory if it is not part of a direct parent directory
        # (i.e. directories are not at the root, or there is a .venv directory)
        let stats = ($local_directory | split row '/' | uniq --count)
        let dist = ($stats | find dist)
        let node_modules = ($stats | find --regex "^node_modules$")
        let venv = ($stats | find --regex "^.venv$")
        if ($venv | is-not-empty) {
            continue
        }
        if ($dist | is-not-empty) and ($node_modules | is-not-empty) {
            continue
        }
        if (($dist | is-not-empty) and ($dist | get count.0) > 1) or (($node_modules | is-not-empty) and ($node_modules | get count.0) > 1) {
            continue
        }

        # Skip the build directory if there is no configuration file at the root
        if not ($"($local_directory)/../($PACKAGE_FILE)" | path exists) {
            continue
        }

        if not $quiet {
            print $"Cleaning the directory ($local_directory)"
        }

        if $dry_run {
            continue
        }

        rm -r $local_directory
    }
}

def main []: nothing -> nothing {}
