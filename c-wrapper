#!/usr/bin/env nu
#
# Copyright (c) Jérémy Audiger.
# All rights reserved.
#

use mod.nu *
use std

const CMAKE_CACHE_FILE = "CMakeCache.txt"
const CMAKE_DEPS_FOLDER = "_deps"

# Cleanup all the cache directories of the current directory recursively
def "main cache cleanup" [
    --path: string=$GIT_FOLDER # The path to clean
    --dry-run # Do not cleanup the cache directories
    --quiet # Do not print the build directories cleaned
]: nothing -> nothing {
    # Local cleanup
    let local_directories = glob $"($path)/**/($CMAKE_CACHE_FILE)"

    for local_directory in $local_directories {
        let local_path = ($local_directory | path dirname)

        # Skip the directory if it is not part of a direct parent directory
        let stats = ($local_path | split row '/' | uniq --count)
        let deps_folder = ($stats | find --regex $"^($CMAKE_DEPS_FOLDER)$")
        if ($deps_folder | is-not-empty) {
            continue
        }

        if not $quiet {
            print $"Cleaning the directory ($local_path)"
        }

        if $dry_run {
            continue
        }

        rm -r $local_path
    }
}

def main []: nothing -> nothing {}
